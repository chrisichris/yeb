/// put module documentation here

module org.yeb.mvc.handlerSpec;

import org.springframework.mock.web:MockServletContext, MockHttpServletRequest, MockHttpServletResponse, MockFilterChain;

load org.yeb.std;
load org.yeb.yebspec;

load org.yeb.mvc.handler;

load org.yeb.validation;

specificationsFor "org.yeb.mvc.handler" \(

    specification "of a handler which takes more than the context" \(
        ct = mockCtxt [Param ["foo":"2","str":"pstr"], Params ["faa":["1","2"], "mix":["name","2"]]];
        describe "handler with user" \(
            should "get the right user" \(
                ha ctxt = (_ = ctxt.user;Completed "");
                rh = regexHandler (All "/foo") ha;
                rh (ct with {user = "foo"});
            );
        );
    );


    specification "of smale handleradapters" \(




        describe "regexHandler" \(
            should "match a regex and put parameters in request" \(
                var p1 = -1;
                var p2 = "";
                rh = regexHandler (All ":num/*") do c:
                    p1 := param "num" c |> (valiNum 0 1000 . strToNum);
                    p2 := param "rest" c;
                    Completed "";
                done;
                r = rh (mockCtxt [Get "23/foo"]);
                assertEquals (Completed "") r;
                assertEquals 23 p1;
                assertEquals p2 "foo";

            );

            should "not call handler and return notHandled if no match" \(
                var called = false;
                rh = regexHandler (All "index") 
                    do: called := true; Completed "" done;
                assertEquals notHandled (rh (mockCtxt [Get "/"]));
                assertFalse called;
            );

            should "respect method " \(
                var called = false;
                rh = regexHandler (Post "index") do:
                    called := true;
                    Completed ""
                done;

                assertEquals notHandled (rh (mockCtxt [Get "index"]));
                assertFalse called;

                assertEquals (Completed "") (rh (mockCtxt [Post "index"]));
                assertTrue called;

            );

            should "take any method if all method given" \(
                var called = false;
                rh = regexHandler (All 'index') do:
                    called := true;
                    Completed "";
                done;

                assertEquals (Completed "") (rh (mockCtxt [Get "index"]));
                assertTrue called;

                called := false;
                assertEquals (Completed "") (rh (mockCtxt [Post "index"]));
                assertTrue called;

            );

            should "set as servletPath the unmatched part" \(
                var sp = "";
                var pi = "";
                rh = regexHandler (All 'index/*') do c:
                    sp := (request c)#getServletPath();
                    pi := (request c)#getPathInfo();
                    Completed "";
                done;

                _ = rh (mockCtxt [Get "index/foo"]);
                assertEquals sp "index/";
                assertEquals pi "foo";

                _ = rh (mockCtxt [Get "index/"]);
                assertEquals sp "index/";
                assertEquals pi "";
            );
           
        );

     );

)

